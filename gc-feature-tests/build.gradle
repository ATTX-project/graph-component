plugins {
    id "java"
    id "idea"
    id "com.chrisgahlert.gradle-dcompose-plugin" version "0.8.2"
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

apply plugin: 'maven'

repositories {
  maven {
    url "https://plugins.gradle.org/m2/"
  }
  maven {
      url "http://attx-dev.hulib.helsinki.fi:8081/repository/attx-snapshots"
  }
}

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}


dependencies {
    testCompile \
            'junit:junit:4.10',
            'info.cukes:cucumber-java8:1.2.5', 
            'info.cukes:cucumber-junit:1.2.5',
            'com.mashape.unirest:unirest-java:1.4.9',
            'org.skyscreamer:jsonassert:1.4.0',
            'net.javacrumbs.json-unit:json-unit:1.16.0',
            'net.javacrumbs.json-unit:json-unit-fluent:1.16.0',
            //'org.apache.jena:apache-jena-libs:3.1.1',
            'org.elasticsearch:elasticsearch:1.2.2',
            'org.uh.attx.gc:gc-rdf2json-indexer:1.0-SNAPSHOT'
    // https://mvnrepository.com/artifact/org.apache.jena/apache-jena
    compile group: 'org.apache.jena', name: 'apache-jena', version: '3.1.1'


            
}
// in case we want have a ./gradlw command.
task wrapper(type: Wrapper) {
   gradleVersion = '3.3'
}


// services needed for the tests

dcompose {
    createComposeFile.useTags = true    
    registry ('http://attx-dev:5000') {}
    registry ('http://attx-dev.hulib.helsinki.fi:5000') {}

    fuseki {
        image = 'stain/jena-fuseki'
        exposedPorts = ['3030']
        portBindings = ['3030:3030']
    }   
    essiren {
        image = 'attx-dev.hulib.helsinki.fi:5000/essiren:latest'
        exposedPorts = ['9200', '9300']
        portBindings = ['9200:9200', '9300:9300']
    }
    es5 {
        image = 'attx-dev:5000/attx-es5:latest'
        exposedPorts = ['9210', '9310']
        portBindings = ['9210:9210', '9310:9310']
    }
    gmapi {
        image = 'attx-dev:5000/gm-api:latest'
        exposedPorts = ['4302']
        portBindings = ['4302:4302']            
        
    }    
    test {
        ignoreExitCode = true
        baseDir = file('.')
        dockerFilename = 'Dockerfile'        
        binds = ["/var/run/docker.sock:/run/docker.sock"]
        command = ['sh', '-c', '/tmp/runTests.sh']
        dependsOn = [gmapi, essiren, fuseki, es5]
        waitForCommand = true
    }
    

}

task copyReportFiles(type: DcomposeCopyFileFromContainerTask) {
    service = dcompose.test       
    containerPath = '/tmp/build/reports'
    destinationDir = file("build/from-container/")
    cleanDestinationDir = false  
}

startTestContainer.finalizedBy('copyReportFiles')

// making sure the that fresh build of test classes is done before building the image
buildTestImage.dependsOn('shadowJar')
buildTestImage.dependsOn('testClasses')


shadowJar {
  classifier = 'tests'
  from sourceSets.test.output
  configurations = [project.configurations.testRuntime]
}
